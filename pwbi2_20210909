

agregovana data = 

ADDCOLUMNS(
SUMMARIZE(
    data, -- tabulka, ze ktere bereme data
    data[Index],data[Rok], -- sloupce, podle kterych agregujeme
    "Srazky",SUM(data[PRCP]), -- novy sloupec definovany funkci SUM()
    "Max. teplota",MAX(data[TMAX]) -- novy sloupec definvoany funkci MAX()
    ) ,
 -- za pomoci funkce ADDCOLUMNS pridavame novy sloupec tabulky,
 -- jehoz navratova hodnota je podminena hodnotou sloupce agregovane tabulky   
"Tropicky den",
IF([Max. teplota] >= 34,"Ano","Ne")
)


    
    ---------------------
    
    tabulka datum cas = 
ADDCOLUMNS(
CROSSJOIN(
CALENDARAUTO(),
UNION(
    ROW("Cas",TIME(9,0,0)),
    ROW("Cas",TIME(12,0,0))
)),"Datum a cas", [Date]+[Cas])


-----------------------------

suma vybrany index + index 9 v danem roce = 

-- suma za uzivatelem vybrany index
VAR tento_rok = sUM(data[PRCP])

-- suma za index 9
VAR index_9 = 
SUMX(
    FILTER(ALL(data),
    data[Index] = 9
    && data[Rok] in VALUES(data[Rok])
    ),
data[PRCP])

return index_9 + tento_rok 


-----------------------------------------

_suma srazek mezirocne = 

// -- suma srazek v danem roce a dane lokalite
SUM(data[PRCP])

-- minus
-

-- suma srazek v predchozim roce a dane lokalite
SUMX(
FILTER(
    /*
    Krok 1: Funkce ALL vraci vsechna data z tabulky data,
    tj. rusime vsechny aplikovane filtry
    */
    ALL(data),

    /*
    Krok 2: Funkce FILTER filtruje tabulku vracenou krokem 1,
    aplikujeme filtry na sloupce [Rok] a [Index]

    */
    data[Rok] = MAX(data[Rok])-1 -- max. rok vraci cele cislo, odecitame 1
    -- hledame vsechnz index, ktere zustaly v tabulce dim lokality
    -- pote, co uzivatel vybral hodnotu z prurezu
    && data[Index] IN VALUES ('dim lokality'[Index])),
-- krok 3: tabulku vracenou funkci FILTER() scitame nad sloupcem PRCP
data[PRCP]) 
